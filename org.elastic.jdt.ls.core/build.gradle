plugins {
    id 'com.diffplug.gradle.p2.asmaven'
}
// The jars we'd like to pull from Eclipse's p2 repositories
def eclipseDeps = [
    // The dependencies we actually use
    'org.eclipse.core.runtime',
    'org.eclipse.jdt.ls.core',
    // 'org.eclipse.lsp4j',
    // 'org.eclipse.lsp4j.jsonrpc',
    'org.eclipse.jdt.core',
    'org.eclipse.jdt.core.manipulation',
    'org.eclipse.core.resources',
    'org.eclipse.core.net'
]
// build a maven repo in our build folder containing these artifacts
p2AsMaven {
    group 'p2', {
        repo 'http://download.eclipse.org/lsp4j/updates/releases/'
        repo 'http://download.eclipse.org/jdtls/snapshots/repository/latest/'
        repo 'http://download.eclipse.org/eclipse/updates/4.7'
        repo 'http://download.eclipse.org/technology/m2e/releases'
        eclipseDeps.each { p2.addIU(it) }
        p2.addIU('org.eclipse.lsp4j', '0.4.0.v20180425-1136')
        p2.addIU('org.eclipse.lsp4j.jsonrpc', '0.4.0.v20180425-1136')
        eclipseDeps.each { p2.addIU(it + '.source') }
    }
}

dependencies {
    eclipseDeps.each { compile "p2:${it}:+" }
    compile 'p2:org.eclipse.lsp4j.jsonrpc:0.4.0.v20180425-1136'
    compile 'p2:org.eclipse.lsp4j:0.4.0.v20180425-1136'
    compile 'com.google.guava:guava:26.0-jre'
    compile 'org.apache.commons:commons-lang3:3.5'
}

jar.manifest.attributes('-exportcontents': 'org.elastic.jdt.ls.core')